// This is a tutorial script demonstrating the Reflex programming language.
//
// For detailed instructions and further information see
// https://incapture.atlassian.net/wiki/display/PDT/Rapture+Getting+Started%3A+REFLEX//
//
// Reflex and Rapture are trademarks of Incapture Technologies LLC

SERIES_AUTHORITY="datacapture";
BLOB_AUTHORITY="tutorialBlob";
DOC_AUTHORITY="tutorialDoc";

BLOB_URI="blob://"+BLOB_AUTHORITY;
DOC_URI="document://"+DOC_AUTHORITY;
SERIES_URI="series://"+SERIES_AUTHORITY;

CSV_PATH = ENV.RAPTURE_TUTORIAL_CSV;
if (CSV_PATH == null) do
  CSV_PATH = "introDataInbound.csv";
  println("No CSV specified. Defaulting to "+CSV_PATH);
end

// **********************
// Create repos if needed
// **********************

if (!#blob.blobRepoExists(BLOB_URI)) do
     #blob.createBlobRepo(BLOB_URI, "BLOB {} USING MONGODB {prefix=\""+BLOB_AUTHORITY+"\" }", "REP {} USING MONGODB { prefix=\""+BLOB_AUTHORITY+"\"}");
end
if (!#doc.docRepoExists(DOC_URI)) do
    #doc.createDocRepo(DOC_URI, "NREP {} USING MONGODB {prefix=\""+DOC_AUTHORITY+"\"}");
end
if (!#series.seriesRepoExists(SERIES_URI)) do
    #series.createSeriesRepo(SERIES_URI, "SREP {} USING MONGODB {prefix=\""+SERIES_AUTHORITY+"\"}");
end

// **********************
// If the data blob does not exist then attempt to load it.
// Note that the REPL window cannot access the local file system.
// This will only work when invoked using RaptureRunner.
// **********************

csvfile = file(CSV_PATH, "CSV");
CSV_FILE = csvfile.getNodeName();
rawCsvUri = BLOB_URI+"/"+CSV_FILE;

if (!#blob.blobExists(rawCsvUri)) do
  println("Reading CSV from file " + CSV_PATH);
  
  csvfile --> rawCsvUri;
  println("CSV uploaded to "+ rawCsvUri);
else do
  println("Data has already been uploaded to the BLOB repository as "+rawCsvUri);
end

println("\nDone\n");
return("Done");
